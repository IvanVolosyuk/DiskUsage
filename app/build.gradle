apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'
android {
    compileSdkVersion 31
    buildToolsVersion "31"

    defaultConfig {
        applicationId "com.google.android.diskusage"
        targetSdkVersion 30
        minSdk 21
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            proto {
                srcDir 'src/main/resources'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/Android.mk')
        }
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "diskusage-v${variant.versionName}-${variant.name}.apk"
    }
}

protobuf {
    protoc {
        // Use pre-compile artifact from repository as upstream recommendation
        artifact = 'com.google.protobuf:protoc:3.20.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }

        }
    }
}

dependencies {
    implementation files('src/main/extra/system.jar')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.protobuf:protobuf-javalite:3.20.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'me.zhanghai.android.appiconloader:appiconloader:1.3.1'

    testImplementation 'junit:junit:4.13.1'
}
